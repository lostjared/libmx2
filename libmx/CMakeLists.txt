cmake_minimum_required(VERSION 3.10)
project(libmx2 VERSION 1.14.0)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(OpenGL_GL_PREFERENCE "GLVND")
set(CMAKE_INSTALL_RPATH ${CMAKE_INSTALL_PREFIX}/lib)
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
set(CMAKE_POSITION_INDEPENDENT_CODE ON) 
if(MINGW)
    set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
    set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
    set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
    set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
endif()

include(CheckIncludeFile)
include(CheckIncludeFileCXX)
include(CheckFunctionExists)
include(CheckLibraryExists)
include(CheckSymbolExists)
include(CheckCXXSourceCompiles)

option(DEBUG_MODE "Debug mode" OFF)
if(DEBUG_MODE)
  add_compile_definitions(DEBUG_MODE)
  message(STATUS "Debug Enabled")
  set(CMAKE_BUILD_TYPE Debug)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -g")
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -g")
else() 
  set(CMAKE_BUILD_TYPE Release)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -O3")
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -O3")
  message(STATUS "Release mode enabled") 
endif()

if(UNIX AND NOT APPLE)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
endif()

add_compile_options(-Wall)

message(STATUS "")
message(STATUS "=== Checking System Headers ===")
check_include_file_cxx(iostream HAVE_IOSTREAM)
check_include_file_cxx(string HAVE_STRING)
check_include_file_cxx(vector HAVE_VECTOR)
check_include_file_cxx(memory HAVE_MEMORY)
check_include_file_cxx(fstream HAVE_FSTREAM)
check_include_file_cxx(sstream HAVE_SSTREAM)
check_include_file_cxx(optional HAVE_OPTIONAL)
check_include_file_cxx(thread HAVE_THREAD)
check_include_file_cxx(mutex HAVE_MUTEX)
check_include_file_cxx(atomic HAVE_ATOMIC)
check_include_file_cxx(functional HAVE_FUNCTIONAL)
check_include_file_cxx(unordered_map HAVE_UNORDERED_MAP)
check_include_file_cxx(queue HAVE_QUEUE)
check_include_file_cxx(condition_variable HAVE_CONDITION_VARIABLE)
check_include_file_cxx(set HAVE_SET)
check_include_file_cxx(array HAVE_ARRAY)
check_include_file_cxx(algorithm HAVE_ALGORITHM)
check_include_file_cxx(stdexcept HAVE_STDEXCEPT)

set(MISSING_STD_HEADERS "")
if(NOT HAVE_IOSTREAM)
  list(APPEND MISSING_STD_HEADERS "iostream")
endif()
if(NOT HAVE_STRING)
  list(APPEND MISSING_STD_HEADERS "string")
endif()
if(NOT HAVE_VECTOR)
  list(APPEND MISSING_STD_HEADERS "vector")
endif()
if(NOT HAVE_MEMORY)
  list(APPEND MISSING_STD_HEADERS "memory")
endif()
if(NOT HAVE_FSTREAM)
  list(APPEND MISSING_STD_HEADERS "fstream")
endif()
if(NOT HAVE_OPTIONAL)
  list(APPEND MISSING_STD_HEADERS "optional (C++17 required)")
endif()
if(NOT HAVE_THREAD)
  list(APPEND MISSING_STD_HEADERS "thread")
endif()
if(NOT HAVE_MUTEX)
  list(APPEND MISSING_STD_HEADERS "mutex")
endif()

if(MISSING_STD_HEADERS)
  message(FATAL_ERROR "Missing required C++ standard headers: ${MISSING_STD_HEADERS}. "
                      "Please ensure you have a C++20 compliant compiler installed.")
endif()
message(STATUS "All required C++ standard headers found.")

check_include_file(stdlib.h HAVE_STDLIB_H)
check_include_file(stdio.h HAVE_STDIO_H)
check_include_file(stdint.h HAVE_STDINT_H)
check_include_file(string.h HAVE_STRING_H)

if(NOT HAVE_STDLIB_H OR NOT HAVE_STDIO_H OR NOT HAVE_STDINT_H)
  message(FATAL_ERROR "Missing required C standard headers (stdlib.h, stdio.h, stdint.h)")
endif()
message(STATUS "All required C standard headers found.")

message(STATUS "")
message(STATUS "=== Checking C++20 Compatibility ===")

check_cxx_source_compiles("
#include <version>
#if __cplusplus < 202002L
#error C++20 required
#endif
int main() { return 0; }
" HAVE_CXX20)

if(NOT HAVE_CXX20)
  message(WARNING "C++20 support may not be fully available. Some features might not compile correctly.")
else()
  message(STATUS "C++20 support confirmed.")
endif()

message(STATUS "")
message(STATUS "=== Checking Required Dependencies ===")

find_package(SDL2 REQUIRED)
if(SDL2_FOUND)
    message(STATUS "SDL2 found: ${SDL2_VERSION}")
    if(TARGET SDL2::SDL2)
        message(STATUS "  Using SDL2::SDL2 target")
    elseif(TARGET SDL2::SDL2-static)
        add_library(SDL2::SDL2 ALIAS SDL2::SDL2-static)
    endif()
else()
    message(FATAL_ERROR "SDL2 not found. Please install mingw-w64-sdl2")
endif()

if(NOT TARGET SDL2::SDL2)
    message(FATAL_ERROR "SDL2 target not found. Ensure mingw-w64-sdl2 is installed correctly.")
endif()


find_package(SDL2_ttf REQUIRED)
message(STATUS "SDL2_ttf found")


find_package(ZLIB REQUIRED)
if(ZLIB_FOUND)
  message(STATUS "ZLIB found: ${ZLIB_VERSION_STRING}")
  message(STATUS "  Include dirs: ${ZLIB_INCLUDE_DIRS}")
  message(STATUS "  Libraries: ${ZLIB_LIBRARIES}")
else()
  message(FATAL_ERROR "ZLIB not found. Please install zlib development package.\n"
                      "  Ubuntu/Debian: sudo apt install zlib1g-dev\n"
                      "  Fedora: sudo dnf install zlib-devel\n"
                      "  Arch: sudo pacman -S zlib\n"
                      "  macOS: brew install zlib")
endif()


set(CMAKE_REQUIRED_INCLUDES ${ZLIB_INCLUDE_DIRS})
check_include_file(zlib.h HAVE_ZLIB_H)
if(NOT HAVE_ZLIB_H)
  message(FATAL_ERROR "zlib.h header not found.")
endif()


find_package(PNG REQUIRED)
if(PNG_FOUND)
  message(STATUS "PNG found: ${PNG_VERSION_STRING}")
  message(STATUS "  Include dirs: ${PNG_INCLUDE_DIRS}")
  message(STATUS "  Libraries: ${PNG_LIBRARIES}")
else()
  message(FATAL_ERROR "libpng not found. Please install libpng development package.\n"
                      "  Ubuntu/Debian: sudo apt install libpng-dev\n"
                      "  Fedora: sudo dnf install libpng-devel\n"
                      "  Arch: sudo pacman -S libpng\n"
                      "  macOS: brew install libpng")
endif()

set(CMAKE_REQUIRED_INCLUDES ${PNG_INCLUDE_DIRS})
check_include_file(png.h HAVE_PNG_H)
if(NOT HAVE_PNG_H)
  message(FATAL_ERROR "png.h header not found.")
endif()

set(PNG_LIBS ${PNG_LIBRARIES} ${ZLIB_LIBRARIES})
set(PNG_DIRS ${ZLIB_INCLUDE_DIRS} ${PNG_INCLUDE_DIRS})
set(SDL2_TTF_INCLUDE_DIRS ${SDL2_TTF_INCLUDE_DIRS})
set(SDL2_TTF_LIBRARIES SDL2_ttf::SDL2_ttf)

include(GNUInstallDirs)
set(INSTALL_INCLUDE_DIR ${CMAKE_INSTALL_INCLUDEDIR}/mx2)
set(INSTALL_LIB_DIR ${CMAKE_INSTALL_LIBDIR})

message(STATUS "")
message(STATUS "=== Optional Features Configuration ===")

option(EXAMPLES "Build Example Projects" ON)
option(JPEG "Build with JPEG Support" OFF)        
option(OPENGL "Build with OpenGL Support" ON)
option(ES3 "Build with OpenGL ES3 Support" OFF)
option(MIXER "Build with Sound Support" ON)
option(VULKAN "Build with Vulkan Support" OFF)
option(MOLTEN "Build with MoltenVK Support" OFF)
option(CMD "Build with libcmd Support" OFF)
option(CROSS "Build Cross Glue" OFF)
option(CV "OpenCV" OFF)
include_directories(${CMAKE_BINARY_DIR} ${CMAKE_INSTALL_PREFIX}/include)

if(CMD)
message(STATUS "libcmd: Enabled")
else()
message(STATUS "libcmd: Disabled")
endif()

if(MIXER)
  message(STATUS "")
  message(STATUS "=== Checking SDL2_mixer ===")
  find_package(SDL2_mixer REQUIRED)
  message(STATUS "SDL2_mixer found")
  
  set(CMAKE_REQUIRED_INCLUDES ${SDL2_MIXER_INCLUDE_DIRS})
  check_include_file(SDL_mixer.h HAVE_SDL_MIXER_H)
  check_include_file(SDL2/SDL_mixer.h HAVE_SDL2_SDL_MIXER_H)
  if(NOT HAVE_SDL_MIXER_H AND NOT HAVE_SDL2_SDL_MIXER_H)
    message(WARNING "SDL_mixer.h header not found in standard paths - continuing anyway.")
  endif()
  
  set(MIXER_INCLUDE ${SDL2_MIXER_INCLUDE_DIRS})
  set(MIXER_LIB SDL2_mixer::SDL2_mixer)
  message(STATUS "Added SDL_mixer support")
  message(STATUS "  MIXER_INCLUDE: ${MIXER_INCLUDE}")
  message(STATUS "  MIXER_LIB: ${MIXER_LIB}")
else()
  message(STATUS "SDL_mixer disabled.")
endif()

if(OPENGL)
  message(STATUS "")
  message(STATUS "=== Checking OpenGL and GLM ===")
  
  find_package(OpenGL REQUIRED)
  message(STATUS "OpenGL found")
  message(STATUS "  OpenGL include dirs: ${OPENGL_INCLUDE_DIR}")
  message(STATUS "  OpenGL libraries: ${OPENGL_gl_LIBRARY}")
  
  find_package(glm REQUIRED)
  message(STATUS "GLM found")
  
  check_include_file_cxx(glm/glm.hpp HAVE_GLM_HPP)
  if(NOT HAVE_GLM_HPP)
    set(CMAKE_REQUIRED_INCLUDES ${GLM_INCLUDE_DIRS})
    check_include_file_cxx(glm/glm.hpp HAVE_GLM_HPP_WITH_PATH)
    if(NOT HAVE_GLM_HPP_WITH_PATH)
      message(WARNING "glm/glm.hpp header check failed - continuing anyway.")
    endif()
  endif()
  
  if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/libmx/src/glad.c")
    message(STATUS "GLAD source found (bundled)")
  else()
    message(WARNING "GLAD source (glad.c) not found in libmx/src/. OpenGL may not work correctly.")
  endif()
  
  set(GL_INCLUDE ${OpenGL_INCLUDE_DIRS} ${GLM_INCLUDE_DIRS})
  set(GL_LIB ${OPENGL_gl_LIBRARY})
  set(GL_SRC src/glad.c console.cpp)
  message(STATUS "Added OpenGL/GLM Support")
  message(STATUS "  GL INCLUDE: ${GL_INCLUDE}")
  message(STATUS "  GL LIB: ${GL_LIB}")
  
  if(ES3)
    message(STATUS "")
    message(STATUS "=== Checking OpenGL ES3 ===")
    message(STATUS "OpenGL ES3 Support Enabled.")
    # Check for GLES3 headers
    check_include_file(GLES3/gl3.h HAVE_GLES3_GL3_H)
    check_include_file(GLES3/gl3platform.h HAVE_GLES3_PLATFORM_H)
    check_include_file(GLES3/gl32.h HAVE_GLES3_GL32_H)
    check_include_file(GLES3/gl31.h HAVE_GLES3_GL31_H)
    if(NOT HAVE_GLES3_GL3_H)
      message(FATAL_ERROR "GLES3/gl3.h header not found. OpenGL ES3 support requires GLES3 headers.\n"
                          "  Ubuntu/Debian: sudo apt install libgles2-mesa-dev\n"
                          "  Fedora: sudo dnf install mesa-libGLES-devel\n"
                          "  Arch: sudo pacman -S mesa\n"
                          "  Or disable ES3 with: -DES3=OFF")
    endif()
    
    message(STATUS "  GLES3/gl3.h: Found")
    if(HAVE_GLES3_PLATFORM_H)
      message(STATUS "  GLES3/gl3platform.h: Found")
    endif()
    if(HAVE_GLES3_GL31_H)
      message(STATUS "  GLES3/gl31.h: Found")
    endif()
    if(HAVE_GLES3_GL32_H)
      message(STATUS "  GLES3/gl32.h: Found")
    endif()
  endif()
else()
  message(STATUS "OpenGL Support Disabled.")
endif()

if(VULKAN)
  message(STATUS "")
  message(STATUS "=== Checking Vulkan ===")
  
  if(MOLTEN)
    message(FATAL_ERROR "Cannot enable both VULKAN and MOLTEN. Choose one:\n"
                        "  -DVULKAN=ON -DMOLTEN=OFF  (for standard Vulkan)\n"
                        "  -DVULKAN=OFF -DMOLTEN=ON  (for MoltenVK on macOS)")
  endif()
  
  set(CMAKE_PREFIX_PATH "$ENV{VULKAN_SDK}")
  find_package(Vulkan QUIET)
  
  if(NOT Vulkan_FOUND)
    message(WARNING "Vulkan not found. Disabling Vulkan support.\n"
                    "  To enable Vulkan, install the Vulkan SDK:\n"
                    "  Arch: sudo pacman -S vulkan-headers vulkan-icd-loader\n"
                    "  Ubuntu/Debian: sudo apt install libvulkan-dev\n"
                    "  Fedora: sudo dnf install vulkan-devel\n"
                    "  Or set VULKAN_SDK environment variable")
    set(VULKAN OFF)
  else()
    message(STATUS "Vulkan found")
    message(STATUS "  Vulkan include dirs: ${Vulkan_INCLUDE_DIRS}")
    message(STATUS "  Vulkan libraries: ${Vulkan_LIBRARIES}")
  
    set(CMAKE_REQUIRED_INCLUDES ${Vulkan_INCLUDE_DIRS})
    check_include_file(vulkan/vulkan.h HAVE_VULKAN_H)
    if(NOT HAVE_VULKAN_H)
      message(WARNING "vulkan/vulkan.h header check failed - continuing anyway.")
    endif()
  
    set(CMAKE_REQUIRED_INCLUDES ${SDL2_INCLUDE_DIRS})
    check_include_file(SDL2/SDL_vulkan.h HAVE_SDL_VULKAN_H)
    check_include_file(SDL_vulkan.h HAVE_SDL_VULKAN_H_ALT)
    if(NOT HAVE_SDL_VULKAN_H AND NOT HAVE_SDL_VULKAN_H_ALT)
      message(WARNING "SDL_vulkan.h header check failed - continuing anyway.")
    endif()
  
    if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/libmx/volk.c" OR EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/libmx/src/volk.c")
      message(STATUS "Volk source found (bundled)")
    else()
      message(WARNING "volk.c not found. Vulkan loading may not work correctly.")
    endif()
  
    find_package(glm REQUIRED)
  
    set(VK_INCLUDE ${Vulkan_INCLUDE_DIRS} ${GLM_INCLUDE_DIRS})
    if(UNIX AND NOT APPLE)
      set(VK_LIB Vulkan::Vulkan)
    else()
      set(VK_LIB "")
    endif()
    set(VK_SRC vk.cpp volk.c)
    message(STATUS "  Vulkan Include: ${VK_INCLUDE}")
    message(STATUS "  Vulkan Lib: ${VK_LIB}")
  endif()
else()
  message(STATUS "Vulkan Support Disabled.")
endif()

if(MOLTEN)
  message(STATUS "")
  message(STATUS "=== Checking MoltenVK ===")
  
  if(VULKAN)
    message(FATAL_ERROR "Cannot enable both VULKAN and MOLTEN. Choose one:\n"
                        "  -DVULKAN=ON -DMOLTEN=OFF  (for standard Vulkan)\n"
                        "  -DVULKAN=OFF -DMOLTEN=ON  (for MoltenVK on macOS)")
  endif()
  
  if(NOT MOLTEN_PATH)
    set(MOLTEN_PATH /usr/local/opt/molten-vk)
  endif()
  
  if(NOT EXISTS "${MOLTEN_PATH}")
    message(FATAL_ERROR "MoltenVK not found at ${MOLTEN_PATH}.\n"
                        "  Please install MoltenVK or set MOLTEN_PATH:\n"
                        "  macOS: brew install molten-vk\n"
                        "  Or: cmake -DMOLTEN_PATH=/path/to/molten-vk ..\n"
                        "  Or disable with: -DMOLTEN=OFF")
  endif()
  
  if(NOT EXISTS "${MOLTEN_PATH}/include/vulkan/vulkan.h")
    message(FATAL_ERROR "MoltenVK vulkan.h not found at ${MOLTEN_PATH}/include/vulkan/")
  endif()
  
  if(NOT EXISTS "${MOLTEN_PATH}/lib/libMoltenVK.dylib")
    message(FATAL_ERROR "libMoltenVK.dylib not found at ${MOLTEN_PATH}/lib/")
  endif()
  
  find_package(glm REQUIRED)
  
  set(VK_INCLUDE ${MOLTEN_PATH}/include ${GLM_INCLUDE_DIRS})
  set(VK_SRC vk.cpp)
  set(VK_LIB "${MOLTEN_PATH}/lib/libMoltenVK.dylib")
  message(STATUS "MoltenVK found at ${MOLTEN_PATH}")
  message(STATUS "  MoltenVK Include: ${VK_INCLUDE}")
  message(STATUS "  MoltenVK LIB: ${VK_LIB}")
else()
  message(STATUS "MoltenVK Disabled.")
endif()

if(JPEG)
  message(STATUS "")
  message(STATUS "=== Checking JPEG ===")  
  find_package(JPEG REQUIRED)
  message(STATUS "JPEG found: ${JPEG_VERSION}")
  message(STATUS "  JPEG Libraries: ${JPEG_LIBRARIES}")
  message(STATUS "  JPEG Include Dirs: ${JPEG_INCLUDE_DIRS}")
  
  set(CMAKE_REQUIRED_INCLUDES ${JPEG_INCLUDE_DIRS})
  check_include_file(jpeglib.h HAVE_JPEGLIB_H)
  if(NOT HAVE_JPEGLIB_H)
    message(WARNING "jpeglib.h header check failed - continuing anyway.")
  endif()
  
  set(IMG_INCLUDE ${JPEG_INCLUDE_DIRS} ${ZLIB_INCLUDE_DIRS} ${PNG_INCLUDE_DIRS})
  set(IMG_LIB ${JPEG_LIBRARIES} ${PNG_LIBRARIES} ${ZLIB_LIBRARIES})
  message(STATUS "Added JPEG Support")
else()
  message(STATUS "JPEG Disabled")
  set(IMG_LIB ${PNG_LIBRARIES} ${ZLIB_LIBRARIES})
  set(IMG_INCLUDE ${ZLIB_INCLUDE_DIRS} ${PNG_INCLUDE_DIRS})
endif()

add_subdirectory(libmx)

if(EXAMPLES)
  message(STATUS "Building with Example Projects")
  add_subdirectory(test)
  add_subdirectory(controller_test)
  add_subdirectory(input_test)
  add_subdirectory(image_test)
  add_subdirectory(knights_tour)
  add_subdirectory(pong)
  add_subdirectory(asteroids)
  add_subdirectory(blocks)
  add_subdirectory(piece)
  add_subdirectory(matrix)
  add_subdirectory(breakout)
  add_subdirectory(frogger)
  add_subdirectory(mad)
  add_subdirectory(space)
  if(MIXER)
    add_subdirectory(sound_test)
  endif()
  if(JPEG)
    add_subdirectory(jpeg_test)
  endif()
  if(OPENGL)
    add_subdirectory(gl_window)
    add_subdirectory(gl_skeleton)
    add_subdirectory(gl_example)
    add_subdirectory(gl_cube)
    add_subdirectory(gl_pong)
    add_subdirectory(gl_animated_cube)
    add_subdirectory(gl_breakout)
    add_subdirectory(gl_pyramid)
    add_subdirectory(gl_mxmod)
    add_subdirectory(space3d)
    add_subdirectory(gl_surface_test)
    add_subdirectory(gl_ps)
    add_subdirectory(gl_ps2)
    add_subdirectory(gl_ps3)
    add_subdirectory(gl_ps4)
    add_subdirectory(gl_ps3d)
    add_subdirectory(gl_stars3d)
    add_subdirectory(gl_work)
    add_subdirectory(gl_explode)
    add_subdirectory(gl_walk)
    add_subdirectory(gl_walktest)
    add_subdirectory(gl_glitch_cube3)
    if(ES3)
    add_subdirectory(gl_es3)
    add_subdirectory(gl_warp_model)
    endif()
    if(CMD)
    add_subdirectory(mp3d)
    message(STATUS "Building with libcmd Example: mp3d")
    endif()
    add_subdirectory(gl_cubemap)
    add_subdirectory(gl_stencil_test)
    add_subdirectory(gl_blend_shader)
    add_subdirectory(gl_framebuffer)
    add_subdirectory(gl_shade1)
    add_subdirectory(gl_shade2)
    add_subdirectory(gl_shadow)
    add_subdirectory(gl_skybox)
    add_subdirectory(gl_skybox2)
    add_subdirectory(gl_skybox3)
    add_subdirectory(gl_curve)
    add_subdirectory(gl_snow)
    add_subdirectory(gl_fire)
    add_subdirectory(gl_water)
    add_subdirectory(gl_rain)
    add_subdirectory(gl_room)
    add_subdirectory(gl_matrix)
    add_subdirectory(gl_obj_explode)
    add_subdirectory(gl_fog)
    add_subdirectory(gl_glitch)
    add_subdirectory(gl_glitch_cube)
    add_subdirectory(gl_glitch_cube2)
    add_subdirectory(gl_ship)
    add_subdirectory(gl_texture_map)
    add_subdirectory(gl_matrix_room)
    add_subdirectory(gl_matrix_glitch)
    add_subdirectory(gl_matrix_psyche)
    add_subdirectory(gl_blank_model)
    add_subdirectory(gl_intro_screen)
    add_subdirectory(gl_model_explode)
    add_subdirectory(gl_model_glitch)
    add_subdirectory(gl_asteroids)
    add_subdirectory(gl_console)
    add_subdirectory(gl_console_skeleton)
    add_subdirectory(gl_console_full)
    add_subdirectory(gl_object)
    add_subdirectory(gl_glitch_ex)
  endif()

  if(VULKAN OR MOLTEN)
     if(NOT MOLTEN)
       add_subdirectory(volk)
     endif()
     add_subdirectory(vk_tex_cube) 
     add_subdirectory(vk_tex_pyramid)
     add_subdirectory(vk_data)
     add_subdirectory(vk_image)
     add_subdirectory(vk_frag)
     add_subdirectory(vk_borg)
     add_subdirectory(vk_wire)
     add_subdirectory(vk_text)
     add_subdirectory(vk_stars)
     add_subdirectory(vk_pong)
     add_subdirectory(vk_fractal)
     add_subdirectory(vk_raycast)
     add_subdirectory(vk_matrix)
     add_subdirectory(vk_matrix_ray)
     if(CV)
     add_Subdirectory(vk_matrix_cv)
     endif()
  endif()

else()
  message(STATUS "Not Building Examples")
endif()

if(CROSS)
   add_subdirectory(cross)
   add_subdirectory(moon)
   add_subdirectory(vk_cross_stars)
endif()

add_subdirectory(mxmod_compress)

message(STATUS "")
message(STATUS "=== Configuration Summary ===")
message(STATUS "Project: ${PROJECT_NAME} ${PROJECT_VERSION}")
message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "C++ Standard: ${CMAKE_CXX_STANDARD}")
message(STATUS "Install prefix: ${CMAKE_INSTALL_PREFIX}")
message(STATUS "")
message(STATUS "Required Dependencies:")
message(STATUS "  SDL2:     Found (${SDL2_VERSION})")
message(STATUS "  SDL2_ttf: Found")
message(STATUS "  ZLIB:     Found (${ZLIB_VERSION_STRING})")
message(STATUS "  PNG:      Found (${PNG_VERSION_STRING})")
message(STATUS "")
message(STATUS "Optional Features:")
message(STATUS "  OpenGL:   ${OPENGL}")
message(STATUS "  OpenGL ES3: ${ES3}")
message(STATUS "  Vulkan:   ${VULKAN}")
message(STATUS "  MoltenVK: ${MOLTEN}")
message(STATUS "  Mixer:    ${MIXER}")
message(STATUS "  JPEG:     ${JPEG}")
message(STATUS "  libcmd:   ${CMD}")
message(STATUS "  Examples: ${EXAMPLES}")
if(CV)
message(STATUS " OpenCV Enabled")
endif()
message(STATUS "")

if(MIXER)
  set(WITH_MIXER_VAL 1)
else()
  set(WITH_MIXER_VAL 0)
endif()

if(OPENGL)
  set(WITH_OPENGL_VAL 1)
else()
  set(WITH_OPENGL_VAL 0)
endif()

if(ES3) 
  set(WITH_OPENGL_ES3 1)
else()
  set(WITH_OPENGL_ES3 0)
endif()

if(VULKAN)
  set(WITH_VK_VAL 1)
else()
  set(WITH_VK_VAL 0)
endif()


if(MOLTEN)
  set(WITH_MOLTEN_VAL 1)
else()
  set(WITH_MOLTEN_VAL 0)
endif()

if(JPEG)
  set(WITH_JPEG_VAL 1)
else()
  set(WITH_JPEG_VAL 0)
endif()

configure_file(
    ${CMAKE_SOURCE_DIR}/config.h.in
    ${CMAKE_BINARY_DIR}/config.h
)

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/libmx2ConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY AnyNewerVersion
)

configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/libmx2Config.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/libmx2Config.cmake"
    @ONLY
)

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/libmx2Config.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/libmx2ConfigVersion.cmake"
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/libmx2)
